import org.gradle.crypto.checksum.Checksum

plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'distribution'
    id 'signing'
    id("org.gradle.crypto.checksum") version "1.4.0"
    id('com.diffplug.spotless') version '6.0.0'
    id('org.omegat.gradle') version '1.5.9'
}

version = '1.0.1-SNAPSHOT'

omegat {
    version = "5.8.0"
    pluginClass = "org.omegat.connectors.machinetranslators.moses.MosesTranslate"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
}

dependencies {
    packIntoJar 'org.omegat:xmlrpc-client:3.1.3-20230209'
    packIntoJar 'org.omegat:xmlrpc-common:3.1.3-20230209'
    implementation "com.github.ben-manes.caffeine:caffeine:2.9.3"
    implementation "com.github.ben-manes.caffeine:jcache:2.9.3"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:2.35.0'
    testImplementation 'commons-io:commons-io:2.8.0'
}

checkstyle {
    ignoreFailures = true
    toolVersion = '7.1'
}

test {
    useJUnitPlatform()
}

distributions {
    main {
        contents {
            from(jar)
            from('README.md', 'COPYING', 'CHANGELOG.md')
        }
    }
}

spotless {
    enforceCheck false
    java {
        eclipse().configFile 'config/spotless/eclipse-formatting.xml'
        removeUnusedImports()
    }
}

signing {
    if (!findProperty("signing.keyId")) {
        useGpgCmd()
    }
    sign tasks.distZip
    sign tasks.jar
}

tasks.register('checksum', Checksum) {
    dependsOn tasks.distZip
    inputFiles.setFrom([tasks.jar.archiveFile, tasks.distZip.archiveFile])
    outputDirectory.set(layout.buildDirectory.dir("distributions"))
    checksumAlgorithm.set(Checksum.Algorithm.SHA512)
    appendFileNameToChecksum.set(true)
}